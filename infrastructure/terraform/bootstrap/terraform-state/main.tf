terraform {
  required_version = ">= 1.10"

  required_providers {
    azurerm = {
      source  = "hashicorp/azurerm"
      version = "~> 4.0"
    }
  }
}

provider "azurerm" {
  features {}
}
# nosemgrep: terraform.azure.security.storage.storage-queue-services-logging.storage-queue-services-logging
# Exception justifiée : ce Storage Account est exclusivement utilisé comme backend
# Blob Storage pour le Terraform State. Azure Queue Storage n'est pas utilisé par
# GeoSafe ni par le backend Terraform ; l'activation du Storage Analytics logging
# pour Queue n'apporterait donc aucune couverture de logs utile.
resource "azurerm_resource_group" "terraform_state" {
  name     = var.resource_group_name
  location = var.location
}

resource "azurerm_storage_account" "terraform_state" {
  name                     = var.storage_account_name
  resource_group_name      = azurerm_resource_group.terraform_state.name
  location                 = azurerm_resource_group.terraform_state.location
  account_tier             = "Standard"
  account_replication_type = "LRS"

  min_tls_version                 = "TLS1_2"
  public_network_access_enabled   = true
  allow_nested_items_to_be_public = false

  blob_properties {
    versioning_enabled = true
    delete_retention_policy {
      days = 7
    }
    container_delete_retention_policy {
      days = 7
    }
  }
  
  tags = {
    Project     = "GeoSafe"
    Environment = "terraform"
    ManagedBy   = "Terraform"
  }
}

resource "azurerm_storage_container" "terraform_state" {
  name                  = var.container_name
  storage_account_id    = azurerm_storage_account.terraform_state.id
  container_access_type = "private"
}